plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.1'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'com.yzgeneration'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	asciidoctorExt
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-mail'
	implementation 'io.jsonwebtoken:jjwt-api:0.12.6'
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springframework.boot:spring-boot-starter-amqp'
	implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
	implementation 'org.springframework.boot:spring-boot-starter-reactor-netty'
	implementation 'com.google.firebase:firebase-admin:9.4.3'

	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.12.6'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.12.6'
	runtimeOnly 'com.mysql:mysql-connector-j'

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'com.navercorp.fixturemonkey:fixture-monkey-starter:1.1.5'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine'

	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
	testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
}

//submodule
tasks.register('copyGitSubmodule', Copy) {
	from './evc-back-config'
	include '*.yml'
	into './src/main/resources'
}

//RestDocs
ext {
	snippetsDir = file('build/generated-snippets')
}

test {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.named('asciidoctor') {
	// Asciidoctor 태스크 설정
	inputs.dir snippetsDir
	configurations 'asciidoctorExt'

	sources {
		include '**/index.adoc'
	}

	// 경로 베이스 설정
	baseDirFollowsSourceFile()
	dependsOn tasks.test
}

bootJar {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE // 중복 항목을 무시
	dependsOn tasks.named('asciidoctor') // asciidoctor 작업 포함
	from(tasks.named('asciidoctor').get().outputDir) {
		into 'static/docs'
	}
}


